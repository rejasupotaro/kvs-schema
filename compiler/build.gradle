apply plugin: 'java'

targetCompatibility = JavaVersion.VERSION_1_7
sourceCompatibility = JavaVersion.VERSION_1_7

dependencies {
    compile project(":core")
    compile 'com.squareup:javawriter:2.5.0'
    compile 'com.google.auto.service:auto-service:1.0-rc2'
}

group = 'com.rejasupotaro'
archivesBaseName = 'kvs-schema-compiler'
version = '0.0.5'

apply plugin: 'eu.appsatori.fatjar'

fatJar {
    classifier 'fat'
    exclude 'META-INF/*.DSA'
}

apply from: '../java-artifacts.gradle'

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.rejasupotaro'
            artifactId 'kvs-schema-compiler'
            version version
            artifact jar
            artifact fatJar
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                Node root = asNode()
                root.appendNode('name', 'kvs-schema')
                root.appendNode('description', 'Immutable code generation to store key-value data for Android')
                root.appendNode('url', 'https://github.com/rejasupotaro/kvs-schema')

                def issues = root.appendNode('issueManagement')
                issues.appendNode('system', 'github')
                issues.appendNode('url', 'https://github.com/rejasupotaro/kvs-schema/issues')

                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/rejasupotaro/kvs-schema')
                scm.appendNode('connection', 'git@github.com:rejasupotaro/kvs-schema.git')
                scm.appendNode('developerConnection', 'git@github.com:rejasupotaro/kvs-schema.git')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'MIT')
                license.appendNode('url', 'https://raw.githubusercontent.com/rejasupotaro/kvs-schema/master/LICENSE.txt')
                license.appendNode('distribution', 'repo')
            }
        }
    }
}

def getBintrayUserProperty() {
    return hasProperty('bintrayUser') ? bintrayUser : ""
}

def getBintrayApiKeyProperty() {
    return hasProperty('bintrayApiKey') ? bintrayApiKey : ""
}

bintray {
    user = bintrayUserProperty
    key = bintrayApiKeyProperty
    publications = ['mavenJava']
    dryRun = false
    pkg {
        repo = 'maven'
        name = 'kvs-schema-compiler'
        licenses = ['MIT']
    }
}

