apply plugin: 'com.android.library'
apply plugin: 'android-apt'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    compile project(":core")
    compile "com.android.support:support-annotations:21.0.0"
    androidTestApt project(":compiler")
}

group = 'com.rejasupotaro'
archivesBaseName = 'kvs-schema'
version = '0.0.2'

android.libraryVariants.all { variant ->
    if (variant.buildType.isDebuggable()) {
        return; // Skip debug builds.
    }
    task("javadoc${variant.name.capitalize()}", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = System.getenv("ANDROID_HOME") + "/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
    }

    task("bundleJavadoc${variant.name.capitalize()}", type: Jar) {
        description "Bundles Javadoc into zip for $variant.name."
        classifier = "javadoc"
        from tasks["javadoc${variant.name.capitalize()}"]
    }
}

task androidJar(type: Jar) {
    from 'build/intermediates/classes/release'
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidJar
    archives androidSourcesJar
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
    publications {
        mavenAndroid(MavenPublication) {
            groupId 'com.rejasupotaro'
            artifactId 'kvs-schema'
            version version
            artifact androidJar
            artifact androidSourcesJar
            pom.withXml {
                Node root = asNode()
                root.appendNode('name', 'kvs-schema')
                root.appendNode('description', 'Immutable code generation to store key-value data for Android')
                root.appendNode('url', 'https://github.com/rejasupotaro/kvs-schema')

                def issues = root.appendNode('issueManagement')
                issues.appendNode('system', 'github')
                issues.appendNode('url', 'https://github.com/rejasupotaro/kvs-schema/issues')

                def scm = root.appendNode('scm')
                scm.appendNode('url', 'https://github.com/rejasupotaro/kvs-schema')
                scm.appendNode('connection', 'git@github.com:rejasupotaro/kvs-schema.git')
                scm.appendNode('developerConnection', 'git@github.com:rejasupotaro/kvs-schema.git')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'MIT')
                license.appendNode('url', 'https://raw.githubusercontent.com/rejasupotaro/kvs-schema/master/LICENSE.txt')
                license.appendNode('distribution', 'repo')
            }
        }
    }
}

def getBintrayUserProperty() {
    return hasProperty('bintrayUser') ? bintrayUser : ""
}

def getBintrayApiKeyProperty() {
    return hasProperty('bintrayApiKey') ? bintrayApiKey : ""
}

bintray {
    user = bintrayUserProperty
    key = bintrayApiKeyProperty
    publications = ['mavenAndroid']
    dryRun = false
    pkg {
        repo = 'maven'
        name = 'kvs-schema'
        licenses = ['MIT']
    }
}
